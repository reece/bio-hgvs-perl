#!/usr/bin/perl

# N.B. This script is intended for low-volume connections. 
# There is no attempt to make the script thread-safe or to optimize for
# concurrency. 

use strict;
use warnings;

use Data::Dumper;
use File::Basename;
use FindBin;
use Getopt::Long qw(:config gnu_getopt);
use HTTP::Daemon;
use HTTP::Status;
use IO::Pipe;
use Log::Log4perl;
use URI::Escape;
use XML::LibXML;

use lib "$FindBin::RealBin/../lib";
use Bio::HGVS::EnsemblConnection;
use Bio::HGVS::Errors;
use Bio::HGVS::Parser;
use Bio::HGVS::Translator;


my $root = dirname( $FindBin::RealBin );
my $jemappelle = basename( $0 );
my $about_xml = _about_xml(
  'format-version' => 2,
 );
my @path_handlers = (
  [ qr%/version(?:/|$)%    		, \&version_handler   ],
  [ qr%/chr-slice/%  			, \&chr_slice_handler ],
  [ qr%/hgvs/genome-map/%  		, \&genome_map_handler ],
  [ qr%/hgvs/translate/%  		, \&translate_handler ],
  #[ qr%/rs/lookup/% 	 		, \&rs_lookup ],
  #[ qr%/hgvs/validate/%   		, \&validate_handler  ],
 );


my %opts = (
  xml_format => 2, 							# indented, with newlines
  sleeptime => 10,
  n_start_attempts => 10,
  port => $ENV{HGVS_WS_PORT} || 7777,
  queue_size => 5,
);
GetOptions(\%opts,
		   'sleeptime|s=i',
		   'port|p=i'
		  )
  or die("$jemappelle: You've got usage issues, homeboy\n");


my $conf = log4conf(\%opts);
my ($log_fn) = $conf =~ m%filename\s+=\s+(/tmp/.+)%;
Log::Log4perl::init( \$conf );

my $logger = Log::Log4perl->get_logger($jemappelle);
$logger->info("$jemappelle starting...");
$logger->info("log in $log_fn");

$logger->info(sprintf('connecting to Ensembl (%s@%s:%s)...',
					  @{Bio::HGVS::EnsemblConnection::defaults{qw(user host port)}}));
my $ens = Bio::HGVS::EnsemblConnection->new();
$logger->info(sprintf('Ensembl connection established'));

my $vm = Bio::HGVS::Translator->new( $ens );
my $vp = Bio::HGVS::Parser->new();

my $daemon = start_daemon( $opts{n_start_attempts} );
$logger->info("$jemappelle available at ", $daemon->url, "\n" );

while( my $c = $daemon->accept ) {
  process_connection($c);
  $c->close;
}

exit;





sub log4conf {
  my $opts = shift;
  my $u = getpwuid($<);
  my $p = $opts->{port};
  return <<EOF;
log4perl.rootLogger 	         	= DEBUG, Logfile, Screen

log4perl.appender.Logfile       	= Log::Log4perl::Appender::File
log4perl.appender.Logfile.filename 	= /tmp/$jemappelle-$p-$u.log

log4perl.appender.Logfile.layout   	= Log::Log4perl::Layout::PatternLayout
log4perl.appender.Logfile.layout.ConversionPattern = %d %c %F:%L %m%n

log4perl.appender.Screen         	= Log::Log4perl::Appender::Screen
log4perl.appender.Screen.stderr  	= 0
log4perl.appender.Screen.layout 	= Log::Log4perl::Layout::SimpleLayout
EOF
}


sub start_daemon {
  my ($ntries) = @_;
  for(my $i=1; $i<=$ntries; $i++) {
	$daemon = HTTP::Daemon->new(
	  LocalPort => $opts{port},
	  Listen => $opts{queue_size},
	 );
	return $daemon if defined $daemon;
	$logger->debug(sprintf('daemon start attempt %d failed: %s; sleeping %d seconds', 
						   $i, $!, $opts{sleeptime}));
	sleep $opts{sleeptime};
  }
  throw Bio::HGVS::Error(
	sprintf("failed to start daemon after $ntries attempts\n"));
}


sub process_connection {
  my ($c) = @_;
  my $ref = sprintf('%s:%s', $c->peerhost, $c->peerport);
  $logger->debug(sprintf('[%s]: connection received',$ref));

  while ( my $req = $c->get_request ) {		# HTTP::Request
	$logger->debug(sprintf('[%s]: request: %s %s', $ref, $req->method, $req->uri->path));

	if ($req->method ne 'GET') {
	  $c->send_error(RC_FORBIDDEN);
	}

	my @handler_matches = grep { $req->uri->path =~ m%^($_->[0])% } @path_handlers;

	if (not @handler_matches) {
	  $c->send_error(RC_FORBIDDEN);
	  return;
	}

	if ($#handler_matches > 0) {
	  $logger->error( sprintf('%s matches more than one path handler; using first',
							  $req->uri->path) );
	}

	my $ph = $handler_matches[0];
	if (my ($path,$query) = $req->uri->path =~ m%($ph->[0])(.*)%) {
	  my $xml_result;
	  try {
		$xml_result = $ph->[1]($ref,$query);
	  } catch Bio::HGVS::Error with {
		my $e = shift;
		$logger->error($e);
		$xml_result = _xmlify_exception($e);
#	  } otherwise {
#		# FIXME: stacktrace is wrong here
#		my $e = Bio::HGVS::Error->new($@);
#		$logger->error($e);
#		$xml_result = _xmlify_exception($e);
	  };

	  my $xml = XML::LibXML::Element->new("$jemappelle-response");
	  $xml->setAttribute('path', $path);
	  $xml->appendChild($about_xml);
	  $xml->appendChild( $xml_result ) if defined $xml_result;

	  $c->send_response( HTTP::Response->new(
		200,undef,undef,$xml->toString($opts{xml_format})."\n")
	   );
	}
  }

  $logger->debug(sprintf('[%s]: connection closed', $ref));
}



sub version_handler {
  # This used to return version info. Now that version info is returned in
  # all responses, this is just a stub.
  return;
}

sub chr_slice_handler {
  my ($ref,$query) = @_;
  my $q = uri_unescape($query);
  $logger->debug("chr_slice_handler with query=$q");
  my ($chr)   = $q =~ m/chr=(\d+)/;
  if (not defined $chr) {
	throw Bio::HGVS::Error(
	  "Couldn't parse chromosome from query; args like .../chr-slice/?chr=6&start=5&end=6");
  }
  my ($start) = $q =~ m/start=(\d+)/;
  my ($stop)  = $q =~ m/end=(\d+)/;
  $stop ||= $start;
  $logger->debug("fetching [$chr,$start,$stop]");
  my $slice = $ens->{sa}->fetch_by_region('chromosome', $chr, $start, $stop);
  my $e = XML::LibXML::Element->new('chr-slice');
  $e->setAttribute('chromosome',$chr);
  $e->setAttribute('start',$start);
  $e->setAttribute('end',$stop);
  $e->setAttribute('sequence',$slice->seq);
  return $e;
}

sub genome_map_handler {
  my ($ref,$query) = @_;
  my $hgvs = uri_unescape($query);
  my $v = $vp->parse($hgvs);
  $logger->debug(sprintf('[%s]: %s parsed okay; type=%s', $ref, $hgvs, $v->type));

  if ($v->type ne 'c') {
	throw Bio::HGVS::Error->new('moltype ',$v->type, ' not supported (yet)');
  }

  my @g = $vm->convert_cds_to_chr($v);
  return join(
	'',
	"<Coordinates>\n",
	(map { _formatter($_) } @g),
	"</Coordinates>\n"
   );
}


sub _formatter {
  my ($g) = shift;
  my $chr = $Bio::HGVS::Translator::nc_to_chr{$g->ref} || '?';
  sprintf("  <ChromosomalPosition chromosome=\"%d\" start=\"%d\" end=\"%d\" hgvs=\"%s\"/>\n",
		  $chr, $g->loc->start, $g->loc->end, "$g");
}




sub coordinate_xml {
  my ($g) = @_;
  my $chr = $Bio::HGVS::Translator::nc_to_chr{$g->ref} || '?';
  my $e = XML::LibXML::Element->new('genomic-coordinates');
  $e->setAttribute('chromosome',$chr);
  $e->setAttribute('start',$g->loc->start->position);
  $e->setAttribute('end',$g->loc->end->position);
  return $e;
}

sub translate_handler {
  my ($ref,$query) = @_;
  my $hgvs = uri_unescape($query);
  my $v = $vp->parse($hgvs);
  $logger->debug(sprintf('[%s]: %s parsed okay; type=%s', $ref, $hgvs, $v->type));

  my $xml = XML::LibXML::Element->new('translation-results');
  $xml->setAttribute('query',$hgvs);

  if ($v->type eq 'g') {

	$xml->appendChild( coordinate_xml($v) );
	foreach my $c ($vm->convert_chr_to_cds($v)) {
	  my $xc = XML::LibXML::Element->new('cds-variant');
	  $xc->setAttribute('hgvs',"$c");
	  if ($c->loc->is_simple) {
		foreach my $p ($vm->convert_cds_to_pro($c)) {
		  my $xp = XML::LibXML::Element->new('protein-variant');
		  $xp->setAttribute('hgvs',"$p");
		  $xc->appendChild($xp);
		}
	  }
	  $xml->appendChild($xc);
	}

  } elsif ($v->type eq 'c') {

	foreach my $g ($vm->convert_cds_to_chr($v)) {
	  my $xg = XML::LibXML::Element->new('genomic-variant');
	  $xg->setAttribute('hgvs',"$g");
	  $xg->appendChild( coordinate_xml($g) );
	  $xml->appendChild($xg);
	}
	if ($v->loc->is_simple) {
	  foreach my $p ($vm->convert_cds_to_pro($v)) {
		my $xp = XML::LibXML::Element->new('protein-variant');
		$xp->setAttribute('hgvs',"$p");
		$xml->appendChild($xp);
	  }
	}

  } elsif ($v->type eq 'p') {

	foreach my $c ($vm->convert_pro_to_cds($v)) {
	  my $xc = XML::LibXML::Element->new('cds-variant');
	  $xc->setAttribute('hgvs',"$c");
	  foreach my $g ($vm->convert_cds_to_chr($c)) {
		my $xg = XML::LibXML::Element->new('genomic-variant');
		$xg->setAttribute('hgvs',"$g");
		$xg->appendChild( coordinate_xml($g) );
		$xc->appendChild($xg);
	  }
	  $xml->appendChild($xc);
	}

  } else {

	throw Bio::HGVS::TypeError(
	  'Only g, c, and p variant types are supported; your variant is type '.$v->type
	 );

  }

  return $xml;
}





sub _about_xml {
  my %extra_info = @_;
  my %hg_info = _fetch_hg_info();
  my $xml = XML::LibXML::Element->new('about');
  $xml->setAttribute($_,$hg_info{$_}) for grep { m/^(?:changeset|tag|date)$/ } keys %hg_info;
  $xml->setAttribute($_,$extra_info{$_}) for keys %extra_info;
  return $xml;
}


sub _fetch_hg_info {
  my %rv;
  my $hg_in = IO::Pipe->new();
  if (not $hg_in->reader( qw(/usr/bin/hg log -l 1), $FindBin::RealBin )) {
	throw Bio::GenomeCommons::Error($!);
  }
  while ( my $line = <$hg_in> ) {
	if (my ($k,$v) = $line =~ m/^(\w+):\s+(.+)/) {
	  $rv{$k} = $v;
	}
  }
  $rv{'changeset'} =~ s/^\d+://;
  return %rv;
}

sub _xmlify_exception {
  my ($e) = @_;
  my $xml = XML::LibXML::Element->new('error');
  $xml->setAttribute('message', $e->error);
  return $xml;
}
