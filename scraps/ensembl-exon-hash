#!/usr/bin/perl

use strict;
use warnings;

use Bio::EnsEMBL::ApiVersion;
use Bio::EnsEMBL::Registry;
use Digest::MD5 qw(md5_hex);

printf("# Ensembl %s\n", software_version());

my $r = 'Bio::EnsEMBL::Registry';
$r->load_registry_from_db(
	  -user => 'anonymous',
	  -host => 'localhost',
	  # -host => 'ensembldb.ensembl.org',  -port => 5306
	 );

my $ta = $r->get_adaptor( 'homo_sapiens', 'Core', 'Transcript' );

my $next_input = @ARGV ? sub { shift(@ARGV) } : sub { $_=<>;chomp;$_ };
while( my $ac = &$next_input ) {
  my $result;
  eval {
	my ($tx) = @{ $ta->fetch_all_by_external_name($ac) };
	defined $tx || die("No transcript returned");
	my @exons = @{ $tx->get_all_Exons() };
	my $exon_string = join('',map {sprintf("%d,%d\n", $_->start, $_->end)} @exons);
	$result = md5_hex($exon_string);
  };
  if ($@) {
	$result = $@;
	$result =~ s/\n/ /g;
  }
  printf("%s\t%s\n", $ac, $result);
}
